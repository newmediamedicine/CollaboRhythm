<?xml version="1.0"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		creationComplete="creationCompleteHandler(event)"
		addedToStage="addedToStageHandler()"
		removedFromStage="removedFromStageHandler()">
	<s:states>
		<s:State name="debuggingToolsEnabled"/>
		<s:State name="debuggingToolsDisabled"/>
	</s:states>
	<fx:Declarations>
		<s:DateTimeFormatter id="dateFormatter" dateTimePattern="MM/dd/yyyy"/>
	</fx:Declarations>
	<fx:Script><![CDATA[
		import collaboRhythm.shared.controller.IApplicationControllerBase;
		import collaboRhythm.shared.model.Account;
		import collaboRhythm.shared.view.MenuIcon;

		import mx.core.DPIClassification;

		import mx.core.FlexGlobals;
		import mx.events.FlexEvent;

		[Bindable]
		protected var _tabletApplicationController:IApplicationControllerBase;

		[Bindable]
		private var _isControllerReady:Boolean;
		protected var _activeAccount:Account;
		[Bindable]
		protected var _activeRecordAccount:Account;
		private var _trackingUrl:String;

		[Embed('/assets/images320dpi/btn_check_on_holo_dark.png')]
		private static var checkOnIcon320:Class;

		[Embed('/assets/images320dpi/btn_check_off_holo_dark.png')]
		private static var checkOffIcon320:Class;

		[Embed('/assets/images240dpi/btn_check_on_holo_dark.png')]
		private static var checkOnIcon240:Class;

		[Embed('/assets/images240dpi/btn_check_off_holo_dark.png')]
		private static var checkOffIcon240:Class;

		[Embed('/assets/images160dpi/btn_check_on_holo_dark.png')]
		private static var checkOnIcon160:Class;

		[Embed('/assets/images160dpi/btn_check_off_holo_dark.png')]
		private static var checkOffIcon160:Class;

		public static var checkOnIcon:Class;
		public static var checkOffIcon:Class;

		private function reloadPluginsViewMenuItem_clickHandler():void
		{
			_tabletApplicationController.reloadPlugins();
		}

		private function demoDateMenuItem_clickHandler(index:int):void
		{
			_tabletApplicationController.useDemoPreset(index);
		}

		private function demoModeMenuItem_clickHandler():void
		{
			if (_tabletApplicationController.settings.demoModeEnabled)
			{
				_tabletApplicationController.targetDate = null;
			}
			else
			{
				_tabletApplicationController.useDemoPreset(0);
			}
		}

		private function createDemoDateLabel(isControllerReady:Boolean, index:int):String
		{
			if (isControllerReady)
			{
				return dateFormatter.format(_tabletApplicationController.settings.demoDatePresets[index]);
			}
			return "";
		}

		public function get isControllerReady():Boolean
		{
			return _isControllerReady;
		}

		public function set isControllerReady(value:Boolean):void
		{
			_isControllerReady = value;
		}

		private function selectRecordMenuItem_clickHandler(event:MouseEvent):void
		{
			_tabletApplicationController.showSelectRecordView();
		}

		private function quitButton_clickHandler(event:MouseEvent):void
		{
			_tabletApplicationController.exitApplication("quit menu button");
		}

		private function aboutButton_clickHandler(event:MouseEvent):void
		{
			_tabletApplicationController.showAboutApplicationView();
		}

		private function toggleFastForwardMenuItem_clickHandler(event:MouseEvent):void
		{
			_tabletApplicationController.fastForwardEnabled = !_tabletApplicationController.fastForwardEnabled;
		}

		[Bindable]
		public function get tabletApplicationController():IApplicationControllerBase
		{
			return _tabletApplicationController;
		}

		public function set tabletApplicationController(value:IApplicationControllerBase):void
		{
			_tabletApplicationController = value;

			if (_tabletApplicationController)
			{
				if (_tabletApplicationController.settings.debuggingToolsEnabled)
				{
					currentState = "debuggingToolsEnabled";
				}
				else
				{
					currentState = "debuggingToolsDisabled";
				}

				isControllerReady = true;
			}
		}

		public function set activeAccount(activeAccount:Account):void
		{
			_activeAccount = activeAccount;
		}

		public function set activeRecordAccount(activeRecordAccount:Account):void
		{
			_activeRecordAccount = activeRecordAccount;
		}

		private function menuButton_clickHandler(event:MouseEvent):void
		{
			FlexGlobals.topLevelApplication.viewMenuOpen = true;
		}

		public function homeButton_clickHandler():void
		{
			_tabletApplicationController.navigateHome();
		}

		protected function addedToStageHandler():void
		{
			stage.addEventListener(KeyboardEvent.KEY_UP, stage_keyUpHandler);
		}

		protected function removedFromStageHandler():void
		{
			stage.removeEventListener(KeyboardEvent.KEY_UP, stage_keyUpHandler);
		}

		protected function stage_keyUpHandler(event:KeyboardEvent):void
		{

		}

		private function determineEnableVoidingMenuItemLabel(adherenceVoidingEnabled:Boolean):String
		{
			if (adherenceVoidingEnabled)
			{
				return "Disable Voiding";
			}
			else
			{
				return "Enable Voiding";
			}
		}

		public function get trackingUrl():String
		{
			return _trackingUrl;
		}

		public function set trackingUrl(value:String):void
		{
			_trackingUrl = value;
		}

		private function creationCompleteHandler(event:FlexEvent):void
		{
			if (checkOnIcon == null)
			{
				switch (FlexGlobals.topLevelApplication.applicationDPI)
				{
					case DPIClassification.DPI_320:
					{
						checkOnIcon = checkOnIcon320;
						checkOffIcon = checkOffIcon320;
						break;
					}
					case DPIClassification.DPI_240:
					{
						checkOnIcon = checkOnIcon240;
						checkOffIcon = checkOffIcon240;
						break;
					}
					default:
					{
						checkOnIcon = checkOnIcon160;
						checkOffIcon = checkOffIcon160;
					}
				}
			}
		}
		]]></fx:Script>
	<s:viewMenuItems>
		<s:ViewMenuItem label="{createDemoDateLabel(_isControllerReady, 0)}" includeIn="debuggingToolsEnabled"
						click="demoDateMenuItem_clickHandler(0)"/>
		<s:ViewMenuItem label="{createDemoDateLabel(_isControllerReady, 1)}" includeIn="debuggingToolsEnabled"
						click="demoDateMenuItem_clickHandler(1)"/>
		<s:ViewMenuItem label="{createDemoDateLabel(_isControllerReady, 2)}" includeIn="debuggingToolsEnabled"
						click="demoDateMenuItem_clickHandler(2)"/>
		<s:ViewMenuItem label="{createDemoDateLabel(_isControllerReady, 3)}" includeIn="debuggingToolsEnabled"
						click="demoDateMenuItem_clickHandler(3)"/>
		<s:ViewMenuItem label="Demo Mode" includeIn="debuggingToolsEnabled"
						icon="{_tabletApplicationController.settings.demoModeEnabled ? checkOnIcon : checkOffIcon}"
						iconPlacement="right"
						click="demoModeMenuItem_clickHandler()"/>
		<s:ViewMenuItem
				label="Fast Forward"
				icon="{_tabletApplicationController &amp;&amp; _tabletApplicationController.fastForwardEnabled ? checkOnIcon : checkOffIcon}"
				iconPlacement="right"
				includeIn="debuggingToolsEnabled"
				click="toggleFastForwardMenuItem_clickHandler(event)"/>
		<s:ViewMenuItem label="Select Record" id="selectRecordMenuItem" click="selectRecordMenuItem_clickHandler(event)"
						visible="{_tabletApplicationController.settings.isClinicianMode}"
						includeInLayout="{_tabletApplicationController.settings.isClinicianMode}"/>
		<s:ViewMenuItem id="enableVoidingMenuItem"
						label="Voiding"
						icon="{_tabletApplicationController.settings.adherenceVoidingEnabled ? checkOnIcon : checkOffIcon}"
						iconPlacement="right"
						visible="{_activeAccount.accountId == _activeRecordAccount.accountId &amp;&amp; _tabletApplicationController.settings.includeAdherenceVoidingMenuItem}"
						includeInLayout="{_activeAccount.accountId == _activeRecordAccount.accountId &amp;&amp; _tabletApplicationController.settings.includeAdherenceVoidingMenuItem}"
						click="_tabletApplicationController.settings.adherenceVoidingEnabled = !_tabletApplicationController.settings.adherenceVoidingEnabled"/>
		<s:ViewMenuItem label="Reload Plugins" includeIn="debuggingToolsEnabled"
						click="reloadPluginsViewMenuItem_clickHandler()"/>
		<s:ViewMenuItem label="About" id="aboutButton" click="aboutButton_clickHandler(event)"/>
		<s:ViewMenuItem label="Quit" id="quitButton" click="quitButton_clickHandler(event)"/>
	</s:viewMenuItems>
	<s:actionContent>
		<s:Button id="menuButton" click="menuButton_clickHandler(event)" icon="{MenuIcon}"/>
	</s:actionContent>
</s:View>
